const LOCALID_ATTENDANT = 1
const LOCALID_MARTEMPLOYEE = 2

mapscripts FirwealdCity_TournamentHallPreBattleRoom_MapScripts {
    MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE [
        VAR_TEMP_0, 0: FirwealdCity_TournamentHallPreBattleRoom_EventScript_SetUpObjects
    ]
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_TOURNAMENT_HALL_ROUND, 0: FirwealdCity_TournamentHallPreBattleRoom_EventScripts_EnterRoom
        VAR_TEMP_1, 0: FirwealdCity_TournamentHallPreBattleRoom_EventScripts_InformPlayer
    ]
}

script FirwealdCity_TournamentHallPreBattleRoom_EventScript_SetUpObjects {
    if (var(VAR_TOURNAMENT_HALL_ROUND) == 0) {
        setobjectxy(LOCALID_ATTENDANT, 3, 7)
        setobjectmovementtype(LOCALID_ATTENDANT, MOVEMENT_TYPE_FACE_UP)
    } else {
        setobjectxy(LOCALID_ATTENDANT, 3, 2)
        setobjectmovementtype(LOCALID_ATTENDANT, MOVEMENT_TYPE_FACE_DOWN)
    }
    turnobject(OBJ_EVENT_ID_PLAYER, DIR_NORTH)
}

script FirwealdCity_TournamentHallPreBattleRoom_EventScripts_EnterRoom {
    applymovement(LOCALID_ATTENDANT, FirwealdCity_TournamentHallPreBattleRoom_Movement_AttendantWalkToDoor)
    applymovement(OBJ_EVENT_ID_PLAYER, FirwealdCity_TournamentHallPreBattleRoom_Movement_PlayerWalkToDoor)
    waitmovement(0)
    setvar(VAR_TOURNAMENT_HALL_ROUND, 1)
    goto(FirwealdCity_TournamentHallPreBattleRoom_EventScripts_InformPlayer)
}

script FirwealdCity_TournamentHallPreBattleRoom_EventScripts_InformPlayer {
    setvar(VAR_TEMP_1, 1)
    if (var(VAR_TOURNAMENT_HALL_ROUND) == 1) {
        loadspeechtail(FALSE, LOCALID_ATTENDANT)
        msgbox(format("In this room, you are able to prepare yourself for your upcoming battles. \pIf you wish to continue to the next round, or to retreat from the challenge, just talk to me."))
        destroyspeechtail
        closemessage
    }
    call(FirwealdCity_TournamentHallPreBattleRoom_EventScripts_ComingRound)
    loadspeechtail(FALSE, LOCALID_ATTENDANT)
    msgbox("I'll be waiting right here.")
    destroyspeechtail
    closemessage
    release
    end
}

script FirwealdCity_TournamentHallPreBattleRoom_EventScripts_ComingRound {
    loadspeechtail(FALSE, LOCALID_ATTENDANT)
    switch (var(VAR_TOURNAMENT_HALL_ROUND)) {
        case 1:
            msgbox("Your first round of the TOURNAMENT\n"
                   "is about to begin.")
            break
        case 2:
            msgbox("Your second round of the TOURNAMENT\n"
                   "is about to begin.")
            break
        case 3:
            msgbox("The semifinals of the TOURNAMENT\n"
                   "are about to begin.")
            break
        case 4:
            msgbox("The finals of the TOURNAMENT\n"
                   "are about to begin.")
            break
    }
    destroyspeechtail
    closemessage
    return
}

script FirwealdCity_TournamentHallPreBattleRoom_EventScripts_Attendant {
    call(FirwealdCity_TournamentHallPreBattleRoom_EventScripts_ComingRound)
    loadspeechtail(FALSE, LOCALID_ATTENDANT)
    message(FirwealdCity_TournamentHallLobby_Text_WhatWouldYouLikeToDo)
    waitmessage
    multichoice(21, 6, MULTI_TOURNAMENT_HALL_PRE_BATTLE, 1)
    switch (var(VAR_RESULT)) {
        case 0: // ENTER
            switch (var(VAR_TOURNAMENT_HALL_ROUND)) {
                case 1:
                    msgbox("Are you ready for your\n"
                           "first battle?", MSGBOX_YESNO)
                    break
                case 2:
                    msgbox("Are you ready for your\n"
                           "second battle?", MSGBOX_YESNO)
                    break
                case 3:
                    msgbox("Are you ready for the\n"
                           "semifinale?", MSGBOX_YESNO)
                    break
                case 4:
                    playse(SE_PIN)
                    applymovement(LOCALID_ATTENDANT, Common_Movement_ExclamationMark)
                    waitmovement(0)
                    msgbox("It seems you've made it to\n"
                           "the FINALE!\p"
                           "Are you ready for your\n"
                           "last battle?", MSGBOX_YESNO)
                    break
            }
            switch (var(VAR_RESULT)) {
                case YES:
                    msgbox("I wish you good luck\n"
                           "on your battle!")
                    destroyspeechtail
                    closemessage
                    if (var(VAR_FACING) == DIR_NORTH)
                    {
                        applymovement(LOCALID_ATTENDANT, FirwealdCity_TournamentHallPreBattleRoom_Movement_AttendantWalkAwayFromDoorNorth)
                        waitmovement(0)
                        applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkUp)
                        waitmovement(0)
                        opendoor(3, 1)
                        playse(SE_SLIDING_DOOR)
                        waitdooranim
                        applymovement(OBJ_EVENT_ID_PLAYER, FirwealdCity_TournamentHallPreBattleRoom_Movement_PlayerWalkIntoDoor)
                        waitmovement(0)
                        closedoor(3, 1)
                        waitdooranim
                    }
                    elif (var(VAR_FACING) == DIR_EAST)
                    {
                        applymovement(LOCALID_ATTENDANT, FirwealdCity_TournamentHallPreBattleRoom_Movement_AttendantWalkAwayFromDoorEast)
                        waitmovement(0)
                        applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkRight)
                        waitmovement(0)
                        opendoor(3, 1)
                        playse(SE_SLIDING_DOOR)
                        waitdooranim
                        applymovement(OBJ_EVENT_ID_PLAYER, FirwealdCity_TournamentHallPreBattleRoom_Movement_PlayerWalkIntoDoor)
                        waitmovement(0)
                        closedoor(3, 1)
                        waitdooranim
                    }
                    elif (var(VAR_FACING) == DIR_WEST)
                    {
                        applymovement(LOCALID_ATTENDANT, FirwealdCity_TournamentHallPreBattleRoom_Movement_AttendantWalkAwayFromDoorWest)
                        waitmovement(0)
                        applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkLeft)
                        waitmovement(0)
                        opendoor(3, 1)
                        playse(SE_SLIDING_DOOR)
                        waitdooranim
                        applymovement(OBJ_EVENT_ID_PLAYER, FirwealdCity_TournamentHallPreBattleRoom_Movement_PlayerWalkIntoDoor)
                        waitmovement(0)
                        closedoor(3, 1)
                        waitdooranim
                    }
                    warpsilent(MAP_FIRWEALD_CITY_TOURNAMENT_HALL_BATTLE_ROOM, 0xFF, 7, 5)
                    waitstate
                case NO:
                    msgbox("I'll be waiting right here.")
                    destroyspeechtail
                    closemessage
                    break
            }
        case 1: // RETIRE
            msgbox("Do you wish to retire from\n"
                   "the TOURNAMENT CHALLENGE?", MSGBOX_YESNO)
            switch (var(VAR_RESULT)) {
                case YES:
                    msgbox("Please follow me back to\n"
                           "the LOBBY.")
                    destroyspeechtail
                    warp(MAP_FIRWEALD_CITY_TOURNAMENT_HALL_LOBBY, 0xFF, 6, 6)
                    waitstate
                case NO:
                    destroyspeechtail
                    goto(FirwealdCity_TournamentHallPreBattleRoom_EventScripts_Attendant)
            }
        case 2: // EXIT
            msgbox("I'll be waiting right here.")
            destroyspeechtail
            closemessage
            break
    }
    release
    end
}

script FirwealdCity_TournamentHallPreBattleRoom_EventScripts_BulletinBoard {
    msgbox("CONTESTANTS:\n"
           "MITOCHON: Newcoming CAMPER.\l"
           "DAISY: One with flowers.\l"
           "SINEAD: Avid BUG CATCHER.\l"
           "WALTER: Prodigy from HOENN!\l"
           "JAXON: GRASS-type collector.\l"
           "ARIANA: Lass from JOHTO!\l"
           "OLIVE: Youngster from KANTO!\l"
           "GORDON: Bought his way in.\l"
           "{PLAYER}: Newcoming challenger!", MSGBOX_SIGN)
    end
}

script FirwealdCity_TournamentHallPreBattleRoom_EventScripts_MartEmployee {
    lock
    faceplayer
    loadspeechtail(FALSE, LOCALID_MARTEMPLOYEE)
    message("Welcome to the TOURNAMENT SHOP.\n"
	        "How can I help you?")
    waitmessage
    pokemart(FirwealdCity_TournamentHallPreBattleRoom_Pokemart)
    destroyspeechtail
    msgbox("Good luck on your\n"
           "TOURNAMENT CHALLENGE!", MSGBOX_NPC)
    release
    end
}

raw `
FirwealdCity_TournamentHallPreBattleRoom_Pokemart:
    .2byte ITEM_POTION
    .2byte ITEM_ANTIDOTE
    .2byte ITEM_PARALYZE_HEAL
    .2byte ITEM_ETHER
    .2byte ITEM_NONE
    release
	end
`

movement FirwealdCity_TournamentHallPreBattleRoom_Movement_AttendantWalkToDoor {
    walk_up * 5
    face_down
}

movement FirwealdCity_TournamentHallPreBattleRoom_Movement_PlayerWalkToDoor {
    walk_up * 5
}

movement FirwealdCity_TournamentHallPreBattleRoom_Movement_AttendantWalkAwayFromDoorNorth {
    walk_right
    face_left
}

movement FirwealdCity_TournamentHallPreBattleRoom_Movement_AttendantWalkAwayFromDoorEast {
    walk_right
    face_left
}

movement FirwealdCity_TournamentHallPreBattleRoom_Movement_AttendantWalkAwayFromDoorWest {
    walk_left
    face_left
}

movement FirwealdCity_TournamentHallPreBattleRoom_Movement_PlayerWalkIntoDoor {
    walk_up
    set_invisible
}

text FirwealdCity_TournamentHallLobby_Text_WhatWouldYouLikeToDo {
    "What would you like to do?"
}