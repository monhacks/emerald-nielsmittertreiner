const LOCALID_BUGCATCHER = 4
const LOCALID_ZIGZAGOON = 5
const LOCALID_AIRLOCK_GRUNT_1 = 6
const LOCALID_AIRLOCK_GRUNT_2 = 7
const LOCALID_RICH_BOY = 8

mapscripts Route403_MapScripts {}

script Route403_EventScript_Timmy {
    trainerbattle_single(TRAINER_ROUTE403_TIMMY, Route403_Text_TimmyIntro, Route403_Text_TimmyDefeat)
    msgbox(Route403_Text_TimmyPostBattle, MSGBOX_NPC)
    end
}

script Route403_EventScript_Anna {
    trainerbattle_single(TRAINER_ROUTE403_ANNA, Route403_Text_AnnaIntro, Route403_Text_AnnaDefeat)
    msgbox(Route403_Text_AnnaPostBattle, MSGBOX_NPC)
    end
}

script Route403_EventScript_Warren {
    trainerbattle_single(TRAINER_ROUTE403_WARREN, Route403_Text_WarrenIntro, Route403_Text_WarrenDefeat)
    msgbox(Route403_Text_WarrenPostBattle, MSGBOX_NPC)
    end
}

script Route403_EventScript_BugCatcher {
    lock
    msgbox("I'm training my ZIGZAGOON\n"
           "so we can challenge PHYLLOS\l"
           "one day!", MSGBOX_NPC)
    applymovement(LOCALID_BUGCATCHER, Route403_Movement_YoungsterJumpInPlace)
    applymovement(LOCALID_ZIGZAGOON, Route403_Movement_ZigzagoonJumpInPlace)
    delay(8)
    playmoncry(SPECIES_ZIGZAGOON, 0)
    waitmovement(0)
    release
    end
}

script Route403_EventScript_Zigzagoon {
    lock
    faceplayer
    playmoncry(SPECIES_ZIGZAGOON, 0)
    waitmoncry
    applymovement(LOCALID_ZIGZAGOON, Common_Movement_FaceRight)
    waitmovement(0)
    release
    end
}

script Route403_EventScript_RichBoy {
    lock
    applymovement(LOCALID_RICH_BOY, Route403_Movement_RichBoyJumpInPlace)
    waitmovement(0)
    loadspeechtail(FALSE, LOCALID_RICH_BOY)
    msgbox("Get outta here!\n"
           "You blue collar scum!\p"
           "My daddy's boat is about to arrive!")
    destroyspeechtail
    closemessage
    if (var(VAR_FIRWEALD_CITY_STATE) > 0) {
        applymovement(LOCALID_AIRLOCK_GRUNT_1, Route403_Movement_TeamAirlockGrunt1WalkBack)
        applymovement(LOCALID_RICH_BOY, Route403_Movement_RichBoyWalkToMurenaCity)
        waitmovement(0)
        applymovement(LOCALID_AIRLOCK_GRUNT_1, Route403_Movement_TeamAirlockGruntsWalkToMurenaCity)
        applymovement(LOCALID_AIRLOCK_GRUNT_2, Route403_Movement_TeamAirlockGruntsWalkToMurenaCity)
        waitmovement(0)
        removeobject(LOCALID_RICH_BOY)
        removeobject(LOCALID_AIRLOCK_GRUNT_1)
        removeobject(LOCALID_AIRLOCK_GRUNT_2)
    }
    release
    end
}

script Route403_EventScript_Grunt {
    msgbox("We got some business here, so\n"
           "I'm keeping an eye out.", MSGBOX_NPC)
    end
}

script Route403_EventScript_TrainerTipsSign {
    msgbox("TRAINER TIPS\p"
           "Always make sure you have enough\n"
           "POKéBALLS in your bag!\p"
           "You never know what you'll run into.", MSGBOX_SIGN)
    end
}

movement Route403_Movement_YoungsterJumpInPlace {
    face_left
    jump_in_place_left * 3
}

movement Route403_Movement_ZigzagoonJumpInPlace {
    jump_in_place_right * 3
}

movement Route403_Movement_RichBoyJumpInPlace {
    jump_in_place_right * 2
}

movement Route403_Movement_TeamAirlockGrunt1WalkBack {
    walk_right
    walk_down
    face_up
}

movement Route403_Movement_RichBoyWalkToMurenaCity {
    walk_right * 7
}

movement Route403_Movement_TeamAirlockGruntsWalkToMurenaCity {
    walk_right * 6
}

text Route403_Text_TimmyIntro {
    "My CLEFAIRY wields the power\n"
    "of the stars!"
}

text Route403_Text_TimmyPostBattle {
    "Now where did I keep that MOON STONE…?"
}

text Route403_Text_TimmyDefeat {
    "Guess sometimes you should\p"
    "get your head out of the stars\p"
    "and come down to earth…"
}

text Route403_Text_AnnaIntro {
    "Sleep tight and don't let the\n"
    "bed bugs bite!"
}

text Route403_Text_AnnaDefeat {
    "Hwwwaahhhh.... Nooo JIGGLYPUFF!\p"
    "Don't put ME to sleep!"
}

text Route403_Text_AnnaPostBattle {
    "Maybe I should get into\n"
    "CONTESTS instead…"
}

text Route403_Text_WarrenIntro {
    "You'll regret coming round\n"
    "here soon enough!\p"
    "Go RATTATA!\n"
    "Bite where it stings!"
}

text Route403_Text_WarrenDefeat {
    "The only place that stings now\p"
    "is my heart…"
}

text Route403_Text_WarrenPostBattle {
    "Fight me again when we're stronger!"
}
